
0 ERRLINESCREEN0 REM KEEP THIS LINE FOR TOOLSHED !!!!
2 REM 5/10/21 VC DLINK ROUTER AS GATEWAY, LAPTOP AS WEB SERVER 
4 REM FIXED 2k TX & RX BUFFER, SOCKET 0 ONLY
6 REM
10 CLEAR 2100 : POKE 65497,0 : WIDTH 80 : CLS            'FULL TX BUFFER OK
20 DATA "GET /coco.html HTTP/1.1"
22 DATA "Host: rickLT.conect2020.org"
24 DATA "Accept-Language: en"
26 DATA ""
30 W$=""
32 FOR J = 1 TO 4                                        'PACKET OF LINES
34 READ X$                                          
36 W$ = W$ + X$ + CHR$(13) + CHR$(10)
38 NEXT J 
40 WB=&H4000 : WM=&H7FF : WE=WB+WM                       'WRITE BASE, MASK
50 RB=&H6000 : RM=&H7FF : RE=RB+RM                       'READ BASE, MASK
120 PRINT "** TEST 5100 BUFFER USING HTTP V0.C **" 
130 POKE &HFF68,&H80                                     'RESET 5100
140 POKE &HFF68,&H03                                     'AUTOINC, INDIRECT
1100 REM ***** LOCAL CONFIG *****
1110 POKE &HFF69,0 : POKE &HFF6A,01                      'START AUTOINC
1120 POKE &HFF6B,192 : POKE &HFF6B,168 : POKE &HFF6B,0 : POKE &HFF6B,1  'GATEWY
1130 POKE &HFF6B,255 : POKE &HFF6B,255 : POKE &HFF6B,255 : POKE &HFF6B,0 'SUBNT
1140 POKE &HFF6B,&H5C : POKE &HFF6B,&H26 : POKE &HFF6B,&H0A  'MAC PT 1
1150 POKE &HFF6B,&H01 : POKE &HFF6B,&H02 : POKE &HFF6B,&H03  'MAC PT 2
1160 POKE &HFF6B,192 : POKE &HFF6B,168 : POKE &HFF6B,0 : POKE &HFF6B,7 'MY IP
1200 REM **** REMOTE CONFIG *****
1210 POKE &HFF69,&H04 : POKE &HFF6A,&H00 : POKE &HFF6B, 1 'SN-CR=TCP MODE
1220 POKE &HFF69,&H04 : POKE &HFF6A,&H04                 'SN-PORTR
1230 POKE &HFF6B,&H80 : POKE &HFF6B,RND(&HFF)            'EPHM PORT=80
1240 POKE &HFF69,&H04 : POKE &HFF6A,&H0C                 'SN-DIPR=FOREIGN IP 
1250 POKE &HFF6B,192 : POKE &HFF6B,168 : POKE &HFF6B,0 : POKE &HFF6B,124
1260 POKE &HFF6B,0 : POKE &HFF6B,80                      'PORT VIA AUTOINC
1300 REM *** OPEN SOCKET *****************************
1310 POKE &HFF69,&H04 : POKE &HFF6A,&H01 : POKE &HFF6B, 1   'SN-CR=OPEN SOCKET
1320 POKE &HFF69,&H04 : POKE &HFF6A,&H03 : S = PEEK(&HFF6B) 'SN-SR<-STATUS
1330 IF S <> &H13 THEN 20040                                '<>INIT, BAIL
1340 POKE &HFF69,&H04 : POKE &HFF6A,&H01 : POKE &HFF6B, 4 'TRY CONNECT   
1350 POKE &HFF69,&H04 : POKE &HFF6A,&H03 : C = PEEK(&HFF6B) 'GET STATUS
1360 IF C = 0 THEN 20000                                 'STAT=CLOSED
1370 IF C <> &H17 THEN 1320                              'DO UNTIL SOCK_ESTAB 
1520 SS = 1                                              'SS=STRING START POS
1530 PRINT "** WRITE DATA **" : PRINT W$
1540 WZ = LEN(W$)                                        'WZ = WRITE SIZE
1550 POKE &HFF69,&H04 : POKE &HFF6A,&H20                 'SN-CR
1552 F = (PEEK(&HFF6B)*256)+PEEK(&HFF6B) 
1554 IF F < WZ THEN GOTO 1550                            'LOOP UNTIL FREE SIZE OK
1560 POKE &HFF69,&H04 : POKE &HFF6A,&H22                 'SN-TX-RD
1570 WP = (PEEK(&HFF6B)*256)+PEEK(&HFF6B)                'WP=WRITE POINTER
1572 B = INT(WP/256) : C = WP-(256*B)
1574 D = INT(WM/256) : E = WM-(256*D)
1576 F = B AND D : G = C AND E
1580 WL = ((F*256)+G)+WB                                 'WL=WP AND MASK+BASE
1600 SG = WZ                                             'PROPOSE ALL BYTES 
1800 REM *** WRITE DATA ***
1810 B = INT(WL/256) : C = WL-(256*B)                    'WRITE LOC IN 2 BYTES 
1820 POKE &HFF69,B : POKE &HFF6A,C                       'SET IT
1830 L = LEN(W$)
1840 FOR I = SS TO L
1850 W = ASC(MID$(W$,I,1))
1860 POKE &HFF6B,W
1870 NEXT I
1875 SS = L+1                                            ' NEXT STRING START
1880 POKE &HFF69,&H04 : POKE &HFF6A,&H01 : POKE &HFF6B,&H20  'SN_CR=SEND IT
2500 REM *** START DATA READ ************************** 
2510 PRINT "** WAITING FOR DATA ";   
2520 POKE &HFF69,&H04 : POKE &HFF6A,&H26                 'SN-RX-RSR
2530 RZ = (PEEK(&HFF6B)*256)+PEEK(&HFF6B)                'RZ=READ SIZE
2540 PRINT ".";
2550 IF RZ = 0 THEN 2520 ELSE PRINT                    'DO UNTIL DATA 
2560 SG=RZ                                               'PROPOSED=ALL BYTES
2570 GOSUB 2900                                          'COMPUTE RP, RL
2580 IF RZ <= RE-RL THEN GOTO 2640                       '1 SEG FITS, DO LAST
2590 SG=RZ-RM :  RZ=RZ-SG                                'SEG = REST OF BUFFER                              
2600 GOSUB 2800                                          'PRINT SEGMENT 
2610 SG=RZ-SG
2620 REM *** LAST OR SINGLE SEGMENT ***
2630 GOSUB 2900                                          'COMPUTE RP, RL                                     
2640 GOSUB 2800                                          ' PRINT SEGMENT         
2650 POKE &HFF69,&H04 : POKE &HFF6A,&H28                 'SN-RX-RD    
2652 RZ=RZ+1 : B=INT(RZ/256) : C=RZ-(256*B)              '1 PAST LAST READ       
2654 POKE &HFF6B,B : POKE &HFF6B,C            
2660 POKE &HFF69,&H04 : POKE &HFF6A,&H01 : POKE &HFF6B,&H40 'SN-CR=READ DONE
2670 GOTO 2520                                           'GET MORE DATA
2800 REM *** PRINT DATA ***
2810 B = INT(RL/256) : C = RL-(256*B)                    'READ LOC IN 2 BYTES 
2820 POKE &HFF69,B : POKE &HFF6A,C                       'SET ADDR ON WIZNET
2830 FOR I = 1 TO SG+1 
2840 D = PEEK(&HFF6B)
2850 IF D > 31 AND D < 128 THEN PRINT CHR$(D); 
2860 IF D = 13 THEN PRINT
2870 NEXT I
2880 RETURN
2899 REM *** COMPUTE RP, RL ***
2900 POKE &HFF69,&H04 : POKE &HFF6A,&H28 
2910 RP = (PEEK(&HFF6B)*256)+PEEK(&HFF6B)              'RP = READ POINTER
2912 B = INT(RP/256) : C = RP-(256*B)
2914 D = INT(RM/256) : E = RM-(256*D)
2916 F = B AND D : G = C AND E
2918 RL = ((F*256)+G)+RB                               'RP AND MASK+BASE
2970 RETURN
19999 STOP                                              
20000 REM ******** CONNECT STATUS DUMP *****************                
20010 REM *** TRY AGAIN ***
20020 IF C = &H15 THEN PRINT "SOCK_SYNSENT 
20022 IF C = &H16 THEN PRINT "SOCK_SYNRECV 
20024 IF C = &H18 THEN PRINT "SOCK_FIN_WAIT    closed
20026 IF C = &H1B THEN PRINT "SOCK_TIME_WAIT   closed
20028 IF C = &H1D THEN PRINT "SOCK_LAST_ACK    closed
20030 RETURN
20040 REM *** FATAL ERROR ***
20042 IF C = &H00 THEN PRINT "SOCK_CLOSED 
20044 IF C = &H13 THEN PRINT "SOCK_INIT         TCP ready
20046 IF C = &H14 THEN PRINT "SOCK_LISTEN       TCP waiting
20048 IF C = &H17 THEN PRINT "SOCK_ESTABLISHED  TCP open
20050 IF C = &H1C THEN PRINT "SOCK_CLOSE_WAIT   TCP close request
20052 IF C = &H22 THEN PRINT "SOCK_UDP          UDP open
20054 IF C = &H32 THEN PRINT "SOCK_IPRAW        IPRAW open
20056 IF C = &H42 THEN PRINT "SOCK_MACRAW       MACRAW open
20060 POKE &HFF69,0 : POKE &HFF6A,01 
20070 PRINT
20080 PRINT " GATEWAY"; PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B)
20090 PRINT "  SUBNET"; PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B)
20100 PRINT "MAC ADDR "; HEX$(PEEK(&HFF6B)); " "; HEX$(PEEK(&HFF6B)); " ";
20105 PRINT HEX$(PEEK(&HFF6B));" "; HEX$(PEEK(&HFF6B)); " "; 
20110 PRINT HEX$(PEEK(&HFF6B)); " "; HEX$(PEEK(&HFF6B))
20120 PRINT "   MY IP"; PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B)
20130 POKE &HFF69,&H04 : POKE &HFF6A,&H04
20132 PRINT " MY PORT"; PEEK(&HFF6B) * 256 + PEEK(&HFF6B) 
20140 PRINT
20150 POKE &HFF69,&H04 : POKE &HFF6A,&H0C
20160 PRINT "  REM IP"; PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B); PEEK(&HFF6B)
20170 PRINT "REM PORT"; PEEK(&HFF6B) * 256 + PEEK(&HFF6B)
20180 PRINT "<EOF>"
20200 REM ******** WRITE BUFFER TESTER ******************** 
20210 POKE &HFF69,B : POKE &HFF6A,C 
20220 FOR I = 1 TO L
20230 PRINT CHR$(PEEK(&HFF6B))
20240 NEXT I
20250 GOTO 32000
31999 REM *** CLOSE ********************************
32000 POKE &HFF69,&H00 : POKE &HFF6A,&H01 : POKE &HFF6B,&H00  'CLOSE SOCKET 
